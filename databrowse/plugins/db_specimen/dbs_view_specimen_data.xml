<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" xmlns:dir="http://thermal.cnde.iastate.edu/databrowse/dir" match="specimen:specimen" mode="full">
    <script type="text/javascript">
        var printurl = "<xsl:value-of select="@printbarcode"/>";
        <![CDATA[
            function printbarcode() {
                $("#loading").text("Printing...");
                $("#loading").fadeIn(500);
                $.ajax({
                    type: "POST",
                    url: printurl
                }).done(function(msg) { 
                    alert(msg); 
                    $("#loading").fadeOut(500);
                }); 
            }
        ]]>
    </script>
    <style>
        .fromgroup {
            color: blue;
            font-weight: 900;
        }
        .override {
            color: red;
            font-weight: 900;
        }
    </style>
    <xsl:if test="//@override">
        <p class="notify-msg warning"><strong>Warning:</strong> Elements from group file are being overwritten by elements from the specimen file.</p>
    </xsl:if>
    <div style="float:right; width:300px; margin-left: 15px;">
        <table class="cream" style="width:278px;">
            <thead>
                <tr><th>Specimen Barcode</th></tr>
            </thead>
            <tbody>
                <tr><td style="text-align:center"><img><xsl:attribute name="src"><xsl:value-of select="@barcode"/></xsl:attribute></img><br/>
                <a onclick="printbarcode()" style="cursor:pointer">Print Barcode on Label Printer</a></td></tr>
            </tbody>
        </table>
        <xsl:if test="count(dir:dir/*)">
            <table class="cream">
                <thead>
                    <tr><th colspan="2">Supporting Files</th></tr>
                </thead>
                <tbody>
                    <xsl:apply-templates select="dir:dir/*" mode="directory"/>
                </tbody>
            </table>
        </xsl:if>
    </div>
    <h1>Specimen Details</h1>
    <table class="cream" style="width:399px;">
        <tbody>
            <tr><th style="width:125px;">Specimen ID</th><td><xsl:value-of  select="specimen:specimenid"/></td></tr>
            <tr><th>Specimen Group</th>
            <td>
                <xsl:choose>
                    <xsl:when test="specimen:groupid/@link">
                        <a>
                            <xsl:attribute name="href"><xsl:value-of select="specimen:groupid/@link"/></xsl:attribute>
                            <xsl:value-of select="specimen:groupid/."/>
                        </a>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:value-of select="specimen:groupid/."/>
                    </xsl:otherwise>
                </xsl:choose>
            </td></tr>
        </tbody>
    </table>
    <xsl:apply-templates select="specimen:provenance"/>
    <xsl:apply-templates select="specimen:physicalproperties"/>
    <xsl:apply-templates select="specimen:geometry"/>
    <xsl:apply-templates select="specimen:flawparameters"/>
    <xsl:apply-templates mode="full"/>
    <h3>Face Reference</h3>
    <xsl:call-template name="specimen-references"/>
    <xsl:call-template name="specimen-directions"/>
    <xsl:call-template name="specimen-notes"/>
    <xsl:call-template name="specimen-log"/>
    <xsl:if test="//@override or //@fromgroup">
        <div>
            <xsl:if test="//@override">
                <span class="override">Overwritten Fields</span>&#160;
            </xsl:if>
            <xsl:if test="//@fromgroup">
                <span class="fromgroup">Fields from Group File</span>
            </xsl:if>
        </div>
    </xsl:if>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" match="specimen:provenance">
    <table class="cream" style="width:399px;">
        <thead>
            <tr><th colspan="2">Provenance</th></tr>
        </thead>
        <tbody>
            <tr><th style="width:125px;">Manufactured By</th><td><xsl:apply-templates select="specimen:manufacturedby" mode="checkgroup"/><xsl:value-of  select="specimen:manufacturedby"/></td></tr>
            <tr><th>Source</th><td><xsl:apply-templates select="specimen:source" mode="checkgroup"/><xsl:value-of  select="specimen:source"/></td></tr>
            <tr><th>In Charge</th><td><xsl:apply-templates select="specimen:incharge" mode="checkgroup"/><xsl:value-of  select="specimen:incharge"/></td></tr>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" name="specimen-references">
    <table class="cream"  style="width:399px;">
        <thead>
            <tr><th style="width:125px;">Face</th><th>Description</th></tr>
        </thead>
        <tbody>
            <xsl:for-each select="specimen:reference">
                <tr><th><xsl:value-of  select="@face"/></th><td><xsl:apply-templates select="." mode="checkgroup"/><xsl:value-of select="."/></td></tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" name="specimen-directions">
    <table class="cream"  style="width:399px;">
        <thead>
            <tr><th  style="width:125px;">Direction</th><th>Description</th></tr>
        </thead>
        <tbody>
            <xsl:for-each select="specimen:direction">
                <tr><th><xsl:value-of  select="@name"/></th><td><xsl:apply-templates select="." mode="checkgroup"/><xsl:value-of select="."/></td></tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" match="specimen:coordinatesystem" mode="full">
    <h3>Coordinate System</h3>
    <table class="cream" style="width:399px;">
        <thead>
            <tr><th colspan="2">Axes Definition (<xsl:value-of select="name(specimen:axesdefinition/*[1])"/>)</th></tr>
        </thead>
        <tbody>
            <xsl:for-each select="specimen:axesdefinition/*[1]/*">
                <tr><th style="width:125px;"><xsl:value-of  select="local-name()"/></th><td><xsl:apply-templates select="." mode="checkgroup"/><xsl:value-of select="specimen:direction/@name"/></td></tr>
            </xsl:for-each>
        </tbody>
    </table>
    <table class="cream" style="width:399px;">
        <thead>
            <tr><th colspan="2">Origin (<xsl:value-of select="name(specimen:origin/*[1])"/>)</th></tr>
        </thead>
        <tbody>
            <xsl:for-each select="specimen:origin/*[1]/*">
                <tr><th style="width:125px;"><xsl:value-of  select="local-name()"/></th><td><xsl:apply-templates select="." mode="checkgroup"/><xsl:value-of select="@face"/></td></tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" match="specimen:geometry">
    <table class="cream" style="width:399px;">
        <thead>
            <tr><th colspan="2">Geometry</th></tr>
        </thead>
        <tbody>
            <tr><th style="width:125px;">Shape</th><td><xsl:apply-templates select="specimen:shape" mode="checkgroup"/><xsl:value-of select="specimen:shape/."/></td></tr>
            <xsl:for-each select="specimen:dimension">
                <tr><th><xsl:value-of  select="@direction"/></th><td><xsl:apply-templates select="." mode="checkgroup"/><xsl:value-of select="."/>&#160;<xsl:value-of select="@units"/></td></tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:db="http://thermal.cnde.iastate.edu/databrowse" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" match="specimen:physicalproperties" >
    <table class="cream" style="width:399px;">
        <thead>
            <tr><th colspan="2">Physical Properties</th></tr>
        </thead>
        <tbody>
            <xsl:for-each select="*">
                <xsl:variable name="localname"><xsl:value-of select="local-name()"/></xsl:variable>         
                <tr><th style="width:125px;">
                    <xsl:choose>
                        <xsl:when test="document('')/*/db:propmap/db:entry[@key=$localname]">
                            <xsl:value-of select="document('')/*/db:propmap/db:entry[@key=$localname]"/>
                        </xsl:when>
                        <xsl:otherwise>
                            <xsl:value-of select="$localname"/>                                    
                        </xsl:otherwise>
                    </xsl:choose>
                </th><td><xsl:apply-templates select="." mode="checkgroup"/><xsl:value-of select="."/><xsl:if test="@units">&#160;<xsl:value-of select="@units" /></xsl:if></td></tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<db:propmap xmlns="http://thermal.cnde.iastate.edu/databrowse" xmlns:db="http://thermal.cnde.iastate.edu/databrowse">
    <entry key="material">Material</entry>
    <entry key="YoungsModulus">Young's Modulus</entry>
    <entry key="ShearModulus">Shear Modulus</entry>
    <entry key="YieldStrength">Yield Strength</entry>
    <entry key="PoissonsRatio">Poisson's Ratio</entry>
    <entry key="Density">Density</entry>
    <entry key="ThermalConductivity">Thermal Conductivity</entry>
    <entry key="ThermalDiffusivity">Thermal Diffusivity</entry>
    <entry key="VolumetricHeatCapacity">Volumetric Heat Capacity</entry>
    <entry key="CijMat">Cij Matrix</entry>
</db:propmap>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" match="specimen:flawparameters">
    <table class="cream" style="width:399px;">
        <thead>
            <tr><th colspan="2">Flaw Parameters (<xsl:value-of select="@flawtype"/>)</th></tr>
        </thead>
        <tbody>
            <tr><th style="width:125px;">Index</th><td><xsl:value-of select="specimen:index/."/></td></tr>
            <tr><th>Flaw Size</th><td><xsl:value-of select="specimen:flawsize/."/>&#160;<xsl:choose><xsl:when test="specimen:flawsize/@units"><xsl:value-of select="specimen:flawsize/@units" /></xsl:when><xsl:otherwise><xsl:value-of select="@units"/></xsl:otherwise></xsl:choose></td></tr>
            <tr><th colspan="2">Location</th></tr>
            <tr><th>X</th><td><xsl:value-of select="specimen:location/specimen:xcoordinate/."/>&#160;<xsl:value-of select="specimen:location/specimen:xcoordinate/@units"/></td></tr>
            <tr><th>Y</th><td><xsl:value-of select="specimen:location/specimen:ycoordinate/."/>&#160;<xsl:value-of select="specimen:location/specimen:ycoordinate/@units"/></td></tr>
            <tr><th>Z</th><td><xsl:value-of select="specimen:location/specimen:zcoordinate/."/>&#160;<xsl:value-of select="specimen:location/specimen:zcoordinate/@units"/></td></tr>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen"  name="specimen-log" mode="full">
    <table class="cream full-width">
        <thead>
            <tr><th colspan="4">Action Log</th></tr>
        </thead>
        <tbody>
            <tr>
                <th>Date</th><th>Performed By</th><th>Action</th><th>Notes</th>
            </tr>
            <xsl:for-each select="specimen:actionlog/specimen:actionevent">
                <tr><xsl:apply-templates select="." mode="checkgroup"/>
                    <td><xsl:value-of select="specimen:date"/></td>
                    <td><xsl:value-of select="specimen:perfby"/></td>
                    <td><xsl:value-of select="specimen:action"/></td>
                    <td>
                        <xsl:call-template name="specimen-lf2p">
                            <xsl:with-param name="instr">
                                <xsl:value-of select="specimen:notes"/>
                            </xsl:with-param>
                        </xsl:call-template>
                    </td>
                </tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" name="specimen-notes">
    <div style="float:left; min-width: 399px; max-width:100%">
        <table class="cream" style="width:100%">
            <thead>
                <tr><th>Notes</th></tr>
            </thead>
            <tbody>
                <tr>
                    <td>
                        <xsl:call-template name="specimen-lf2p">
                            <xsl:with-param name="instr">
                                <xsl:value-of select="specimen:notes"/>
                            </xsl:with-param>
                        </xsl:call-template>
                    </td>
                </tr>
            </tbody>
        </table>
    </div>
</xsl:template>
<xsl:template name="specimen-lf2p"> <!-- Convert linefeeds to <p> </p> -->
    <xsl:param name="instr"/>
    <xsl:choose>
        <xsl:when test="contains($instr,'&#x0a;')">
            <p>
                <xsl:value-of select="substring-before($instr,'&#x0a;')"/>
            </p>
            <xsl:call-template name="specimen-lf2p">
                <xsl:with-param name="instr">
                    <xsl:value-of select="substring-after($instr,'&#x0a;')"/>
                </xsl:with-param>
            </xsl:call-template>
        </xsl:when>
        <xsl:otherwise>
            <p>
                <xsl:value-of select="$instr"/>
            </p>
        </xsl:otherwise>
    </xsl:choose>
</xsl:template> 
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:dir="http://thermal.cnde.iastate.edu/databrowse/dir" xmlns:image="http://thermal.cnde.iastate.edu/databrowse/image" match="dir:file" mode="directory">
    <tr>
        <td>
            <a>
                <xsl:attribute name="href"><xsl:value-of select="@link"/></xsl:attribute>
                <img style="vertical-align:text-top;">
                    <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/<xsl:value-of select="@icon"/></xsl:attribute>
                </img>
            </a>
        </td>
        <td>
            <a>
                <xsl:attribute name="href"><xsl:value-of select="@link"/></xsl:attribute>
                <xsl:value-of select="@basename"/></a><br/>
            <xsl:if test="image:image">
                <div style="text-align:center;">
                    <a>
                        <xsl:attribute name="href"><xsl:value-of select="image:image/@href"/></xsl:attribute>
                        <img style="border:1px solid #900">
                            <xsl:attribute name="src"><xsl:value-of select="image:image/@src"/></xsl:attribute>
                        </img></a></div>
            </xsl:if>
        </td>
    </tr>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:specimen="http://thermal.cnde.iastate.edu/specimen" match="specimen:*" mode="full">
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:dir="http://thermal.cnde.iastate.edu/databrowse/dir" match="dir:*" mode="full">
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:dir="http://thermal.cnde.iastate.edu/databrowse/dir" xmlns:image="http://thermal.cnde.iastate.edu/databrowse/image" match="*" mode="checkgroup">
    <xsl:choose>
        <xsl:when test="@fromgroup">
            <xsl:attribute name="class">fromgroup</xsl:attribute>
        </xsl:when>
        <xsl:when test="@override">
            <xsl:attribute name="class">override</xsl:attribute>
        </xsl:when>
        <xsl:otherwise/>
    </xsl:choose>
</xsl:template>


