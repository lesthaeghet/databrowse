<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:dbdg="http://thermal.cnde.iastate.edu/databrowse/dataguzzler" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dbdg:dbdg" mode="full">
	<xsl:element name="script">
    	<xsl:attribute name="type">text/javascript</xsl:attribute>
    	<xsl:attribute name="src"><xsl:value-of select="@resurl"/>/prettify/prettify.js</xsl:attribute>
    </xsl:element>
    <xsl:element name="link">
    	<xsl:attribute name="type">text/css</xsl:attribute>
    	<xsl:attribute name="rel">stylesheet</xsl:attribute>
    	<xsl:attribute name="href"><xsl:value-of select="@resurl"/>/prettify/prettify.css</xsl:attribute>
    </xsl:element>
    <style>
        .dgcode {
            white-space: pre-wrap;
            white-space: -moz-pre-wrap;
            white-space: -pre-wrap;
            white-space: -o-pre-wrap;
            word-wrap: break-word;
            max-width: 555px;
        }
        #wfmlist {
            font-size: small;
            width: 100%;
        }
        #wfmlist li {
            overflow:auto;
            display:inline-block; 
            list-style-type:none; 
            width:120px;
            float: left;
        }
    </style>
    <script type="text/javascript" defer="true">
        <![CDATA[
            $(document).ready(function() {
                prettyPrint();
            });         
        ]]>
    </script>
    <table class="cream full-width">
        <thead>
            <th style="width:8px">
                <img><xsl:attribute name="src"><xsl:value-of select="@resurl"/>/icons/<xsl:value-of select="@icon"/></xsl:attribute></img>
            </th>
            <th colspan="2" style="word-wrap: break-word">
                <span style="width:665px;display:inline-block"><xsl:value-of select="@name"/></span>
                <a>
                    <xsl:attribute name="href"><xsl:value-of select="@downlink"/></xsl:attribute>
                    <img><xsl:attribute name="src"><xsl:value-of select="@resurl"/>/icons/save.png</xsl:attribute></img>
                </a>
            </th>
        </thead>
    </table>
    <xsl:apply-templates/>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dg:SNAPSHTS">
    <xsl:element name="script">
        <xsl:attribute name="type">text/javascript</xsl:attribute>
        <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/DataTables/media/js/jquery.dataTables.min.js</xsl:attribute>
    </xsl:element>
    <script type="text/javascript" defer="true">
        <![CDATA[
            $(document).ready(function() {
                $('#snapshotslist').dataTable({
                    "sScrollX": "764px",
                    "bScrollCollapse": true,
                    "sPaginationType": "full_numbers",
                    "bStateSave": true
                });
            });         
        ]]>
    </script>
    <xsl:element name="link">
        <xsl:attribute name="type">text/css</xsl:attribute>
        <xsl:attribute name="rel">stylesheet</xsl:attribute>
        <xsl:attribute name="href"><xsl:value-of select="$resdir"/>/DataTables/media/css/jquery.dataTables.css</xsl:attribute>
    </xsl:element>
    <style>
        #snapshotslist {
            white-space: nowrap;
        }
    </style>
    <h2>Dataguzzler Snapshots List</h2>
    <table class="cream" id="snapshotslist">
        <thead>
            <tr>
                <th>#</th>
                <xsl:for-each select="dg:SNAPSHOT[1]/dg:METADATA/dg:METDATUM">
                    <th><xsl:value-of select="dg:METDNAME"/></th>
                </xsl:for-each>
            </tr>
        </thead>
        <tbody>
            <xsl:for-each select="dg:SNAPSHOT">
                <tr>
                    <td><a><xsl:attribute name="href">#SNAPSHOT<xsl:number/></xsl:attribute><xsl:number/></a></td>
                    <xsl:for-each select="dg:METADATA/dg:METDATUM">
                        <td><xsl:value-of select="dg:METDNAME/following-sibling::dg:*"/></td>
                    </xsl:for-each>
                </tr>
            </xsl:for-each>
        </tbody>
    </table>
    <br/><br/>
    <hr/>
    <xsl:apply-templates/>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dg:SNAPSHOT">
    <a><xsl:attribute name="name">SNAPSHOT<xsl:number/></xsl:attribute></a>
    <h2>Dataguzzler Snapshot Summary</h2>
    <h5>Jump To:</h5>
    <ul id="wfmlist">
        <xsl:for-each select="dg:GUZZNWFM">
            <xsl:sort data-type="text" select="dg:WAVENAME"/>
            <li><a><xsl:attribute name="href">#SNAPSHOT<xsl:number count="dg:SNAPSHOT"/>-<xsl:value-of select="dg:WAVENAME"/></xsl:attribute><xsl:value-of select="dg:WAVENAME"/></a></li>
        </xsl:for-each>
    </ul>
    <p>&#160;<br/></p>
    <xsl:if test="count(dg:METADATA/dg:METDATUM) > 0">
       <h3>Snapshot Metadata</h3>
       <table class="cream full-width">
            <tbody>
                <xsl:for-each select="dg:METADATA/dg:METDATUM">
                    <xsl:sort select="dg:METDNAME"/>
                    <tr>
                        <th style="width:144px"><xsl:value-of select="dg:METDNAME"/>:</th>
                        <td>
                            <xsl:value-of select="dg:METDNAME/following-sibling::dg:*"/>
                        </td>
                    </tr>
                </xsl:for-each>
            </tbody>
        </table>
    </xsl:if>
    <p><h3>Waveform List</h3></p>
    <xsl:apply-templates/>
    <hr/>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dg:GUZZNWFM">
    <table class="cream full-width">
        <thead>
            <tr>
                <th style="width:144px">Waveform Name:</th>
                <td><a><xsl:attribute name="name">SNAPSHOT<xsl:number count="dg:SNAPSHOT"/>-<xsl:value-of select="dg:WAVENAME"/></xsl:attribute></a><strong><xsl:value-of select="dg:WAVENAME"/></strong><div style="float:right"><img style="vertical-align:text-top"><xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/save.png</xsl:attribute></img>&#160;<a><xsl:attribute name="href"><xsl:value-of select="//@matlink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/></xsl:attribute>MAT</a> | <a><xsl:attribute name="href"><xsl:value-of select="//@csvlink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/></xsl:attribute>CSV</a> | <a><xsl:attribute name="href"><xsl:value-of select="//@dgzlink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/></xsl:attribute>DGZ</a><xsl:if test="substring-before(substring-after(dg:GUZZWFMD/dg:WFMDIMNS/., '&#10;'), '&#10;')='3'"> | <a><xsl:attribute name="href"><xsl:value-of select="//@avilink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/></xsl:attribute>AVI</a></xsl:if></div></td>
            </tr>
        </thead>
        <tbody>
            <xsl:if test="not(dg:GUZZWFMD/dg:DATASVAL)">
                <tr>
                    <th>Waveform Preview:</th>
                    <td style="text-align:center"><img width="500px">
                            
                                <xsl:choose>
                                    <xsl:when test="substring-before(substring-after(dg:GUZZWFMD/dg:WFMDIMNS/., '&#10;'), '&#10;')='3'">
                                        <xsl:choose>
                                            <xsl:when test="dg:GUZZWFMD/dg:METADATA/dg:METDATUM[dg:METDNAME='ProcDimLen3' and dg:METDINTV='150&#10;']">
                                                <xsl:attribute name="src"><xsl:value-of select="//@imagelink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/>&amp;frame=109</xsl:attribute>
                                            </xsl:when>
                                            <xsl:when test="substring-after(substring-after(substring-after(substring-after(dg:GUZZWFMD/dg:WFMDIMNS/., '&#10;'), '&#10;'), '&#10;'), '&#10;')='150&#10;'">
                                                <xsl:attribute name="src"><xsl:value-of select="//@imagelink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/>&amp;frame=109</xsl:attribute>
                                            </xsl:when>
                                            <xsl:otherwise>
                                                <xsl:attribute name="src"><xsl:value-of select="//@imagelink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/>&amp;frame=0</xsl:attribute>
                                            </xsl:otherwise>
                                        </xsl:choose>
                                    </xsl:when>
                                    <xsl:otherwise><xsl:attribute name="src"><xsl:value-of select="//@imagelink"/>&amp;snapshot=<xsl:number count="dg:SNAPSHOT"/>&amp;waveform=<xsl:value-of select="dg:WAVENAME"/></xsl:attribute></xsl:otherwise>
                                </xsl:choose>                        
                        </img>
                    </td>
                </tr>
            </xsl:if>
            <tr>
                <xsl:choose>
                    <xsl:when test="dg:GUZZWFMD/dg:DATASVAL">
                        <th>Scalar Value:</th>
                        <td><xsl:value-of select="dg:GUZZWFMD/dg:DATASVAL"/>&#160;<xsl:value-of select="dg:GUZZWFMD/dg:METADATA/dg:METDATUM[dg:METDNAME='AmplUnits']/dg:METDSTRV"/></td>
                    </xsl:when>
                    <xsl:otherwise>
                        <th>Waveform Size:</th>
                        <td><xsl:value-of select="dg:GUZZWFMD/dg:DATARRYF"/></td>
                    </xsl:otherwise>
                </xsl:choose>
            </tr>
            <tr>
                <th>Dimensions:</th>
                <td><xsl:value-of select="dg:GUZZWFMD/dg:WFMDIMNS"/></td>
            </tr>
            <xsl:for-each select="dg:GUZZWFMD/dg:METADATA/dg:METDATUM">
                <xsl:sort select="dg:METDNAME"/>
                <tr>
                    <th><xsl:value-of select="dg:METDNAME"/>:</th>
                    <td>
                        <xsl:choose>
                            <xsl:when test="dg:METDNAME/. = 'ProcExpr' or dg:METDNAME/. = 'ProcRGBA' or dg:METDNAME/. = 'ProcMD'">
                                <pre class="prettyprint dgcode">
                                    <xsl:value-of select="dg:METDNAME/following-sibling::dg:*"/>
                                </pre>
                            </xsl:when>
                            <xsl:otherwise>
                                <xsl:value-of select="dg:METDNAME/following-sibling::dg:*"/>
                            </xsl:otherwise>
                        </xsl:choose>
                    </td>
                </tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dg:DATAGUZZ">
    <xsl:choose>
        <xsl:when test="count(dg:GUZZWFMD)>1">
            <xsl:element name="script">
                <xsl:attribute name="type">text/javascript</xsl:attribute>
                <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/DataTables/media/js/jquery.dataTables.min.js</xsl:attribute>
            </xsl:element>
            <script type="text/javascript" defer="true">
                <![CDATA[
                    $(document).ready(function() {
                        $('#waveformlist').dataTable({
                            "sScrollX": "764px",
                            "bScrollCollapse": true,
                            "sPaginationType": "full_numbers",
                            "bStateSave": true
                        });
                    });         
                ]]>
            </script>
            <xsl:element name="link">
                <xsl:attribute name="type">text/css</xsl:attribute>
                <xsl:attribute name="rel">stylesheet</xsl:attribute>
                <xsl:attribute name="href"><xsl:value-of select="$resdir"/>/DataTables/media/css/jquery.dataTables.css</xsl:attribute>
            </xsl:element>
            <style>
                #waveformlist {
                    white-space: nowrap;
                }
            </style>
            <h2>Dataguzzler Unnamed Waveform Array Summary</h2>
            <table class="cream" id="waveformlist">
                <thead>
                    <tr>
                        <th>#</th>
                        <th>Waveform Size</th>
                        <th>Dimensions</th>
                        <xsl:for-each select="dg:GUZZWFMD[1]/dg:METADATA/dg:METDATUM">
                            <th><xsl:value-of select="dg:METDNAME"/></th>
                        </xsl:for-each>
                    </tr>
                </thead>
                <tbody>
                    <xsl:for-each select="dg:GUZZWFMD">
                        <tr>
                            <td><a><xsl:attribute name="href">#WAVEFORM<xsl:number/></xsl:attribute><xsl:number/></a></td>
                            <td><xsl:value-of select="dg:DATARRYF"/></td>
                            <td><xsl:value-of select="dg:WFMDIMNS"/></td>
                            <xsl:for-each select="dg:METADATA/dg:METDATUM">
                                <td><xsl:value-of select="dg:METDNAME/following-sibling::dg:*"/></td>
                            </xsl:for-each>
                        </tr>
                    </xsl:for-each>
                </tbody>
            </table>
            <p>&#160;<br/></p>
            <hr/>
            <h3>Waveform List</h3>
            <xsl:apply-templates select="dg:GUZZWFMD"/>
        </xsl:when>
        <xsl:otherwise>
            <h2>Dataguzzler Unnamed Waveform Summary</h2>
            <xsl:apply-templates select="dg:GUZZWFMD"/>
        </xsl:otherwise>
    </xsl:choose>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dg:GUZZWFMD">
    <table class="cream full-width">
        <thead>
            <tr>
                <th>Waveform Number:</th>
                <td><a><xsl:attribute name="name">WAVEFORM<xsl:number/></xsl:attribute></a><xsl:number/><div style="float:right"><img style="vertical-align:text-top"><xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/save.png</xsl:attribute></img>&#160;<a><xsl:attribute name="href"><xsl:value-of select="//@matlink"/>&amp;waveform=<xsl:number/></xsl:attribute>MAT</a> | <a><xsl:attribute name="href"><xsl:value-of select="//@csvlink"/>&amp;waveform=<xsl:number/></xsl:attribute>CSV</a> | <a><xsl:attribute name="href"><xsl:value-of select="//@dgzlink"/>&amp;waveform=<xsl:number/></xsl:attribute>DGZ</a><xsl:if test="substring-before(substring-after(dg:WFMDIMNS/., '&#10;'), '&#10;')='3'"> | <a><xsl:attribute name="href"><xsl:value-of select="//@avilink"/>&amp;waveform=<xsl:number/></xsl:attribute>AVI</a></xsl:if></div></td>
            </tr>
        </thead>
        <tbody>
            <xsl:if test="not(dg:DATASVAL)">
                <tr>
                    <th>Waveform Preview:</th>
                    <td style="text-align:center"><img width="500px">
                            <xsl:attribute name="src"><xsl:value-of select="//@imagelink"/>&amp;waveform=<xsl:number/></xsl:attribute>
                        </img>
                    </td>
                </tr>
            </xsl:if>
            <tr>
                <xsl:choose>
                    <xsl:when test="dg:DATASVAL">
                        <th>Scalar Value:</th>
                        <td><xsl:value-of select="dg:DATASVAL"/>&#160;<xsl:value-of select="dg:METADATA/dg:METDATUM[dg:METDNAME='AmplUnits']/dg:METDSTRV"/></td>
                    </xsl:when>
                    <xsl:otherwise>
                        <th>Waveform Size:</th>
                        <td><xsl:value-of select="dg:DATARRYF"/></td>
                    </xsl:otherwise>
                </xsl:choose>
            </tr>
            <tr>
                <th>Dimensions:</th>
                <td><xsl:value-of select="dg:WFMDIMNS"/></td>
            </tr>
            <xsl:for-each select="dg:METADATA/dg:METDATUM">
                <xsl:sort select="dg:METDNAME"/>
                <tr>
                    <th><xsl:value-of select="dg:METDNAME"/>:</th>
                    <td>
                        <xsl:choose>
                            <xsl:when test="dg:METDNAME/. = 'ProcExpr' or dg:METDNAME/. = 'ProcRGBA' or dg:METDNAME/. = 'ProcMD'">
                                <pre class="prettyprint dgcode">
                                    <xsl:value-of select="dg:METDNAME/following-sibling::dg:*"/>
                                </pre>
                            </xsl:when>
                            <xsl:otherwise>
                                <xsl:value-of select="dg:METDNAME/following-sibling::dg:*"/>
                            </xsl:otherwise>
                        </xsl:choose>
                    </td>
                </tr>
            </xsl:for-each>
        </tbody>
    </table>
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dg:METADATA">
</xsl:template>
<xsl:template xmlns="http://www.w3.org/1999/xhtml" xmlns:dg="http://thermal.cnde.iastate.edu/dataguzzler" match="dg:*">
    <xsl:apply-templates/>
</xsl:template>